cmake_minimum_required (VERSION 3.9)

SET(SYSSP_LIST
     # Name -> lua script file path
     # 
     # Hint: Make sure that the stored procedure name is not longer that 31
     # characters (MAX_SPNAME-1 (31)), i.e. not be longer that the below
     # line. If it is, you'll get a "bad procedure name" error.
     #-------------------------------|
     "sys.physrep.get_reverse_hosts   -> ${CURRENT_SOURCE_DIR}/lib/physrep_get_reverse_hosts.lua"
     "sys.physrep.get_revhosts_v2     -> ${CURRENT_SOURCE_DIR}/lib/physrep_get_revhosts_v2.lua"
     "sys.physrep.keepalive           -> ${CURRENT_SOURCE_DIR}/lib/physrep_keepalive.lua"
     "sys.physrep.register_replicant  -> ${CURRENT_SOURCE_DIR}/lib/physrep_register_replicant.lua"
     "sys.physrep.reset_nodes         -> ${CURRENT_SOURCE_DIR}/lib/physrep_reset_nodes.lua"
     "sys.physrep.should_wait_for_con -> ${CURRENT_SOURCE_DIR}/lib/physrep_should_wait_for_con.lua"
     "sys.physrep.shouldwait_v2       -> ${CURRENT_SOURCE_DIR}/lib/physrep_shouldwait_v2.lua"
     "sys.physrep.topology            -> ${CURRENT_SOURCE_DIR}/lib/physrep_topology.lua"
     "sys.physrep.update_registry     -> ${CURRENT_SOURCE_DIR}/lib/physrep_update_registry.lua"
   )

foreach(SYSSP IN LISTS SYSSP_LIST) 
    if (SYSSP MATCHES "^(.*)->(.*)$")
        string(STRIP ${CMAKE_MATCH_1} SYSSP_NAME)
        string(STRIP ${CMAKE_MATCH_2} SYSSP_PATH)

        file(READ ${SYSSP_PATH} FILE_CONTENT)

        string(REGEX REPLACE "(\")" "\\\\\\1" FILE_CONTENT ${FILE_CONTENT})
        string(REPLACE "\n" "\\n" FILE_CONTENT ${FILE_CONTENT})

        set(CMAKE_SYSSP_LIST ${CMAKE_SYSSP_LIST}{\"${SYSSP_NAME}\",\"${FILE_CONTENT}\",\"${SYSSP_NAME}\"},\n)
    else()
        message(FATAL_ERROR "Invalid system stored procedure entry: ${SYSSP}")
    endif()
endforeach()

configure_file(${CURRENT_SOURCE_DIR}/syssp.c ${CURRENT_BUILD_DIR}/syssp.c @ONLY)
