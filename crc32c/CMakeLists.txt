function(add_crc32c_library TARGET_NAME)
  set(OPTS "WITH_UBSAN")
  set(SINGLE_VALUE_ARGS "")
  set(MULTI_VALUE_ARGS "")
  cmake_parse_arguments(CRC32C "${OPTS}" "${SINGLE_VALUE_ARGS}" "${MULTI_VALUE_ARGS}" ${ARGN})

  set(crc32c_source_dir ${PROJECT_SOURCE_DIR}/crc32c)
  add_library(${TARGET_NAME} ${crc32c_source_dir}/crc32c.c)
  target_include_directories(${TARGET_NAME} PRIVATE ${PROJECT_SOURCE_DIR}/util)
  target_include_directories(${TARGET_NAME} PUBLIC ${crc32c_source_dir})

  if(CMAKE_SYSTEM_PROCESSOR MATCHES "x86_64")
    target_compile_options(${TARGET_NAME} PRIVATE -msse4.2 -mpclmul)
  elseif(CMAKE_SYSTEM_PROCESSOR MATCHES "arm64|armv7l|aarch64")
    separate_arguments(arm_crc_flags NATIVE_COMMAND "${CRC32_ARMV7_C_FLAGS}")
    target_compile_options(${TARGET_NAME} PRIVATE ${arm_crc_flags})
  endif()

  if(CRC32C_WITH_UBSAN)
    target_compile_options(${TARGET_NAME} PRIVATE "-fsanitize=undefined;-fno-omit-frame-pointer")
    target_link_options(${TARGET_NAME} PUBLIC "-fsanitize=undefined")
  endif()
endfunction()

add_crc32c_library(crc32c)
